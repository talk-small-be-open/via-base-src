as yet unclassified
updateRootEssentials: anHtmlRoot

	self requestContext response doNotCache.
	
	anHtmlRoot beHtml5.

"obsolete. Seaside sets it	anHtmlRoot meta
		charset: 'utf-8'."
		
	"Viewport"
	anHtmlRoot meta
		name: 'viewport';
		content: 'width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no'.
	
	"Tell robots not to follow links"
	anHtmlRoot meta
		name: 'ROBOTS';
		content: 'INDEX, NOFOLLOW'.
		
	"Favicon"
	anHtmlRoot link
		beShortcutIcon;
		url: '/favicon.ico'.

	"Alternative favicons"
	VIAHelper faviconImagesData do: [ :each |
		| dimension x |
		x := each first asString.
		dimension := (x, 'x', x).
		anHtmlRoot link
			relationship: 'icon';
			attributeAt: 'sizes' put: dimension;
			url: ('/site/favicons/favicon-', dimension, '.png');
			attributeAt: 'type' put: 'image/png'.
	].

	anHtmlRoot htmlAttributes
		at: 'lang' put: self session languageCode.
		
	self isDevelopment
		ifTrue: [
			anHtmlRoot title: 'VIA DEVELOPMENT ', self session websiteTitle.
			anHtmlRoot bodyAttributes addClass: 'development'.
		]
		ifFalse: [
			anHtmlRoot title: (self session websiteTitle).
		].

	"jQuery, but NO jQuery-UI"
	anHtmlRoot javascript defer; url: '/js/jquery/jquery.min.js'.

	"Underscore JS"
	anHtmlRoot javascript defer; url: '/js/underscore-min.js'.

	"REMARK: We do not defer, because some of it we might use in early JavaScript"
	anHtmlRoot javascript url: '/js/js.cookie.js'.

	"Our own library
	REMARK: We do not defer, because some of it we might use in early JavaScript"
	anHtmlRoot javascript url: '/js/lib.js'.
